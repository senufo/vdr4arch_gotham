pkgname=pearldpf-simple
pkgver=20120306
pkgrel=3
pkgdesc="Graphlcd skin for Pearl Digital Picture Frames"
arch=('any')
url="http://www.vdr-portal.de/board18-vdr-hardware/board11-lcds/p1053917-pearl-display-yavdr-todo-install/#post1053917"
depends=('ttf-ubuntu-font-family' 'vdr-graphlcd')
makedepends=('git')
license=('custom')
source=('git+http://dl.dropbox.com/s/f4gc4243t4pgv98/pearldpf-simple.git')
backup=('usr/share/vdr/plugins/graphlcd/skins/pearldpf-simple/settings.conf')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git log -1 --pretty=format:%ad --date=short | sed 's/-//g'
}

prepare() {
  cd "$srcdir/$pkgname"
  sed -i 's/VDRSymbolsSans/DejaVuSans/g' $pkgname.skin
}

package() {
  mkdir -p "$pkgdir/usr/share/vdr/plugins/graphlcd/skins/$pkgname"
  cp -r "$srcdir/$pkgname/"{$pkgname.skin,icons,inc,locale,styles,settings.conf} \
    "$pkgdir/usr/share/vdr/plugins/graphlcd/skins/$pkgname"

  mkdir -p "$pkgdir/usr/share/vdr/plugins/graphlcd/fonts"
  cd "$pkgdir/usr/share/vdr/plugins/graphlcd/fonts"

  ln -s /usr/share/fonts/TTF/{Ubuntu-B.ttf,Ubuntu-C.ttf,UbuntuMono-B.ttf} .
}
